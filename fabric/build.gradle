plugins {
    id 'multiloader-loader'
    id 'fabric-loom' version '1.7-SNAPSHOT'
}

repositories {
    maven { url "https://maven.terraformersmc.com/releases/" }
    flatDir(dirs: ["$rootDir/libs"])
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modCompileOnly "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modLocalRuntime "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modApi(":dawn-config-$dawn_config_version-fabric") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    include ":dawn-config-$dawn_config_version-fabric"
    modCompileOnly "com.terraformersmc:modmenu:${fabric_modmenu_version}"
    modLocalRuntime "com.terraformersmc:modmenu:${fabric_modmenu_version}"
}

loom {
    def aw = project(":common").file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("runs/client")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("runs/server")
        }
    }
}